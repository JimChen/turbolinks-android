apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

buildscript {
    ext.libVersionName = '1.0.9'

    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '29.0.2'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 11
        versionName "$libVersionName"

        // Define ProGuard rules for this android library project. These rules will be applied when
        // a consumer of this library sets 'minifyEnabled true'.
        consumerProguardFiles 'proguard-consumer-rules.pro'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'org.apache.commons:commons-lang3:3.4'

    testImplementation 'org.assertj:assertj-core:1.7.0'
    testImplementation 'org.robolectric:robolectric:3.1.2'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'junit:junit:4.12'
}

ext {
    bintrayRepo = 'turbolinks-android'
    bintrayName = 'turbolinks-android'

    publishedGroupId = 'com.oscart'
    artifact = 'turbolinks'

    libraryName = 'Turbolinks Android'
    libraryDescription = 'Turbolinks for Android'
    libraryVersion = "$libVersionName"

    siteUrl = 'https://github.com/JimChen/turbolinks-android'
    gitUrl = 'https://github.com/JimChen/turbolinks-android.git'

    developerId = 'oscart'
    developerName = 'Oscart'
    developerEmail = 'kinghotsysu@gmail.com'
}

// Maven
group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription

                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        licenses = ['MIT']
        publish = true
        publicDownloadNumbers = true
        vcsUrl = gitUrl
        version {
            desc = libraryDescription
        }
    }
}
